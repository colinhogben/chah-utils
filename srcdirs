#!/usr/bin/env perl
#=======================================================================
#       List directories containing source files
#=======================================================================
use Getopt::Long;
use Cwd qw(getcwd);

(my $myname = $0) =~ s!.*/!!;
my @args = @ARGV;
my($grep);
my($var) = 'TOP';
my @exclude;
GetOptions('g|grep' => \$grep,
	   'v|var=s' => \$var,
	   'x|exclude=s' => \@exclude)
    and @ARGV <= 1
    or die "Usage: $0 [--grep] [--var=NAME] [--exclude=path]... [dir]\n";
my $top = (@ARGV ? shift : '.');
$top = getcwd if $top eq '.';
$top =~ s!/$!!;
my $toplen = length($top);

my(%patterns,%dirs);
open(my $find, '-|', 'find',$top,
     '(','-name','*.h',
     '-o','-name','*.c',
     '-o','-name','*.hpp',
     '-o','-name','*.cpp',
     ')','-print')
    or die "Cannot open pipe from find: $!\n";
while (<$find>) {
    chomp(my $path = $_);
    my($dir,$ext) = $path =~ m!(.*/).*?(\..*)!;
    die "Bad path $dir\n" unless substr($dir,0,$toplen+1) eq "$top/";
    my $reldir = substr($dir,$toplen+1);
    next if &exclude("/$reldir");
    $dirs{$dir} = 1;
    my $pat = "$reldir*$ext";
    $patterns{$pat} = 1;
}
close($find);

if ($grep) {
    print "#!/bin/sh\n";
    print "# Generated by: $myname @args\n";
    print "$var=$top\n";
    print "exec grep \"\$\@\" \\\n";
    foreach my $pat (sort keys %patterns) {
	print "    \$$var/$pat \\\n";
    }
    print "\n";
} else {
    foreach my $dir (sort keys %dirs) {
	print "$dir\n";
    }
}

sub exclude {
    my($path) = @_;
    foreach my $excl (@exclude) {
	if (excl =~ m!^/!) {
	    return 1 if $path =~ m!^\Q$excl\E/!;
	} else {
	    return 1 if $path =~ m!\Q$excl\E/!;
	}
    }
    return;
}
