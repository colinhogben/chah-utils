#!/usr/bin/env perl
#=======================================================================
#       List directories containing source files
#=======================================================================
use strict;
use Getopt::Long;
use Cwd qw(getcwd);

(my $myname = $0) =~ s!.*/!!;
my @args = @ARGV;
my($mode);
my(@typelists);
my($var) = 'TOP';
my @exclude;
GetOptions('g|grep' => sub {$mode = 'grep'},
	   't|types=s' => \@typelists,
	   'e|etags' => sub {$mode = 'etags'},
	   'v|var=s' => \$var,
	   'x|exclude=s' => \@exclude)
    and @ARGV <= 1
    or die "Usage: $0 [--grep|--etags] [--types=ext1,ext2,...] [--var=NAME] [--exclude=path]... [dir]\n";
my $top = (@ARGV ? shift : '.');
@typelists = ('h,c,hpp,cpp') unless @typelists;
$top = getcwd if $top eq '.';
$top =~ s!/$!!;
my $toplen = length($top);

my @match;
foreach my $type (map {split /,/, $_} @typelists) {
    push @match, '-o' if @match;
    push @match, '-name', "*.$type";
}
my @cmd = ('find',$top, '(', @match, ')', '-print');
my(%patterns,%dirs);
open(my $find, '-|', @cmd)
    or die "Cannot open pipe from find: $!\n";
while (<$find>) {
    chomp(my $path = $_);
    my($dir,$ext) = $path =~ m!(.*/).*?(\..*)!;
    die "Bad path $dir\n" unless substr($dir,0,$toplen+1) eq "$top/";
    my $reldir = substr($dir,$toplen+1);
    next if &exclude("/$reldir");
    $dirs{$dir} = 1;
    my $pat = "$reldir*$ext";
    $patterns{$pat} = 1;
}
close($find);

if ($mode eq 'grep') {
    print "#!/bin/sh\n";
    print "# Generated by: $myname @args\n";
    print "$var=$top\n";
    print "exec grep \"\$\@\" \\\n";
    foreach my $pat (sort keys %patterns) {
	print "    \$$var/$pat \\\n";
    }
    print "\n";
} elsif ($mode eq 'etags') {
    print "#!/bin/sh\n";
    print "$var=$top\n";
    print "exec etags \\\n";
    foreach my $pat (sort keys %patterns) {
	print "    \$$var/$pat \\\n";
    }
    print "\n";
} else {
    foreach my $dir (sort keys %dirs) {
	print "$dir\n";
    }
}

sub exclude {
    my($path) = @_;
    foreach my $excl (@exclude) {
	my $skip;
	if ($excl =~ m!^/!) {
	    # Explicitly at top level
	    $skip = 1 if $path =~ m!^\Q$excl\E/!;
	} else {
	    $skip = 2 if $path =~ m!\Q$excl\E/!;
	}
	if ($skip) {
	    # print STDERR "Excluding $path\n";
	    return 1;
	}
    }
    return;
}
